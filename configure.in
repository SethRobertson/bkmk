dnl To regenerate configure, run `autoconf`  Do NOT run aclocal.
dnl
dnl
dnl ++Copyright BAKA++
dnl
dnl Copyright © 2001-2010 The Authors. All rights reserved.
dnl
dnl This source code is licensed to you under the terms of the file
dnl LICENSE.TXT in this release for further details.
dnl
dnl Send e-mail to <projectbaka@baka.org> for further information.
dnl
dnl - -Copyright BAKA- -
dnl
dnl
dnl Autoconf configuration
dnl
AC_REVISION()dnl
AC_INIT
AC_CONFIG_SRCDIR([libbk_autoconf.h.in])
AC_CONFIG_AUX_DIR(acaux)

echo
echo  "		CONFIGURING BAKA ENVIRONMENT"
echo

AC_PROG_CC

if test "$CC" "!=" "gcc"; then
   AC_MSG_WARN([Attempting to compile without gcc may be painful])
fi

AC_PROG_CXX
AC_PROG_LEX
AC_PROG_YACC
AC_PROG_CPP
AC_PROG_CXXCPP
AC_PROG_CC_C_O
AC_PROG_AWK


# yes, this seems redundant, but we need to call CHECK_LIB so that
# later checks for math routines have the correct -lm options, but
# at the same time, the AC_CHECK_LIBM is auto-magically darwin smart
AC_CHECK_LIB(m, pow)
AC_CHECK_LIBM
AC_SUBST(LIBM)

AC_CHECK_LIB(dl,dlopen)
AC_CHECK_LIB(crypt,crypt)
AC_CHECK_LIB(crypto,CRYPTO_free)
AC_CHECK_LIB(ssl,SSL_CTX_new, /bin/true, /bin/true)
if test "$ac_cv_lib_ssl_SSL_CTX_new" = yes
then
	AC_SUBST(HAVE_LIBSSL,true)
fi

AC_CHECK_LIB(rt,clock_gettime, /bin/true, /bin/true)
if test "$ac_cv_lib_rt_clock_gettime" = yes
then
	AC_SUBST(HAVE_LIBRT,true)
	AC_SUBST(RT_LIBS,-lrt)
fi


AC_CHECK_LIB(krb5support, krb5int_pthread_loaded, /bin/true, /bin/true)
if test "$ac_cv_lib_krb5support_krb5int_pthread_loaded" = yes
then
	AC_SUBST(HAVE_KRB5SUPPORT,true)
fi


AC_CHECK_FILE(/proj/postgres, HAVE_PROJ_POSTGRES=true, HAVE_PROJ_POSTGRES=false)
if test "$HAVE_PROJ_POSTGRES" "=" "true"
then
  AC_DEFINE(HAVE_PROJ_POSTGRES)
fi
AC_SUBST(HAVE_PROJ_POSTGRES)

AC_C_ALIGN_FUNCTIONS
AC_C_NO_POINTER_SIGN

AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(long long)
AC_CHECK_SIZEOF(void *)
AC_CHECK_SIZEOF(time_t)

if test "$ac_cv_prog_ac_ct_CC" = "gcc"
then
  # check for desired level of gcc warnings
  # <TODO>Should be implemented as a single AC_ macro in aclocal.m4</TODO>
  CFLAGS_OLD="$CFLAGS"
  CFLAGS="$CFLAGS -Werror -Wchar-subscripts"
  BK_C_OVERZEALOUS_CHAR_SUBSCRIPTS
  CFLAGS="$CFLAGS_OLD"

  echo $ECHO_N "checking for compiler warning flags... $ECHO_C" >&6
  SAVE_CFLAGS="$CFLAGS"
  CFLAGS="-W -Wunused -Wno-unused-parameter"
  AC_TRY_COMPILE(,,BK_SPECIAL_DEBUG="$CFLAGS",BK_SPECIAL_DEBUG="-W")
  AC_SUBST(BK_SPECIAL_DEBUG)
  CFLAGS="$SAVE_CFLAGS"
  echo "${ECHO_T}$BK_SPECIAL_DEBUG" >&6

  BK_C_SIG_BRAINDAMAGE
fi

# <TODO bugid="999">This macro doesn't work right when cross-compiling; it
# should be replaced by something that greps the generated object file.</TODO>
AC_C_BIGENDIAN

AC_PROG_INSTALL
AC_PROG_LN_S
AC_PATH_PROG(PAWD, pawd, '')

# give precedence to LD_LIBRARY_PATH where possible
AC_ENABLE_PATHENV(first)
# libtool static build doesn't work the baka way anyhow
AC_DISABLE_STATIC

AC_LIBTOOL_WIN32_DLL
AC_LIBTOOL_DLOPEN
AC_PROG_LIBTOOL
AC_CHECK_LIB(ltdl, lt_dladvise_init)

AC_DEFINE_UNQUOTED(SHLIBPATH_VAR, "$shlibpath_var")
AC_SUBST(shlibpath_var)

# expand command(Darwin)/normal variable for module/shared library extension
module=yes
eval modulext=$shrext
unset module
eval shlibext=$shrext
AC_DEFINE_UNQUOTED(SHLIBEXT, "$shlibext")
AC_SUBST(shlibext)
AC_DEFINE_UNQUOTED(MODULEXT, "$modulext")
AC_SUBST(modulext)

AC_SUBST(lt_prog_compiler_static)

AC_C__FUNC__
AC_CONSTRUCTORS
AC_SYS_LARGEFILE
AC_CHECK_HEADERS(paths.h ieeefp.h alloca.h)
AC_CHECK_HEADERS(net/ethernet.h netinet/ether.h  netinet/if_ether.h)
AC_CHECK_HEADERS(sys/capability.h sys/msg.h)
AC_CHECK_HEADERS(sys/ioctl.h sys/filio.h)
AC_CHECK_HEADERS(valgrind/memcheck.h)

AC_CHECK_HEADERS(crt_externs.h)
AC_CHECK_FUNC(_NSGetEnviron, [AC_DEFINE(HAVE_NSGETENVIRON, 1, [Define if your system needs _NSGetEnviron to set up the environment])])

AC_CHECK_HEADERS(mach/vm_map.h)
AC_CHECK_FUNC(vm_region, [AC_DEFINE(HAVE_VM_REGION, 1, [Define for Mach-O systems])])

AC_SA_LEN
AC_SOCKLEN_T
AC_IN_ADDR_T
AC_IN6_MULTICAST
AC_FUNC_INET_PTON

AC_FUNC_MKTIME
if test $ac_cv_func_working_mktime = no; then
  AC_DEFINE(MKTIME_HAS_BUGS)
fi
AC_TIME_MAX(BK_TIME_MAX)

AC_CHECK_TYPE(u_int8_t, uint8_t)
AC_CHECK_TYPE(u_int16_t, uint16_t)
AC_CHECK_TYPE(u_int32_t, uint32_t)
AC_CHECK_TYPE(u_int64_t, uint64_t)

AC_SEARCH_LIBS([aioread64],[aio])
AC_SEARCH_LIBS([socket],[socket])
AC_SEARCH_LIBS([gethostbyname],[resolv nsl])
AC_SEARCH_LIBS([inet_aton],[resolv nsl])

ACX_PTHREAD(
  [HAVE_PTHREADS=true
    AC_CHECK_HEADERS(pthread_np.h, [], [], [
     #include <pthread.h>
    ])
    AC_CHECK_FUNCS(pthread_kill_other_threads_np)
    AC_CHECK_FUNCS(pthread_set_name_np)],
  [HAVE_PTHREADS=false])
AC_SUBST(HAVE_PTHREADS)

dnl <TODO bugid="881">should use getaddressinfo function instead</TODO>>
AC_SEARCH_LIBS([gethostbyname2],[resolv],[AC_DEFINE(HAVE_GETHOSTBYNAME2)])

AC_SEARCH_LIBS([openpty],[c util System],
  [AC_DEFINE(HAVE_OPENPTY)
   AC_CHECK_HEADERS(pty.h util.h libutil.h)]
)

PROFILED_LIBS=""
libprof_src="/tmp/libprof$$.c"
echo 'main(){}' > $libprof_src

dnl Add any potentially existing profiled library from any arch here. We'll figure out
dnl which ones (if any) actually exist.

for profiled_lib in		\
	     BrokenLocale	\
	     anl		\
	     c			\
	     crypt		\
	     dl			\
	     m			\
	     nsl		\
	     pthread		\
	     resolv		\
	     rpcsvc		\
	     rt			\
	     util
     do
	AC_MSG_CHECKING(for existence of lib${profiled_lib}_p)
	if $CC -o /dev/null $libprof_src -l${profiled_lib}_p >&5 2>&1
	then
		PROFILED_LIBS="$PROFILED_LIBS -l${profiled_lib}"
		AC_MSG_RESULT(yes)
	else
		AC_MSG_RESULT(no)
	fi
done
rm -f $libprof_src
AC_SUBST(PROFILED_LIBS)

AC_CHECK_FUNCS(isinf ntohll)
AC_CHECK_FUNCS(memmem memrchr setenv strndup strnlen strverscmp strsignal)
AC_CHECK_FUNCS(strptime gmtime_r localtime_r timegm)
AC_CHECK_FUNCS(getpwuid_r)

AC_CHECK_FUNCS(log2)
AC_CHECK_FUNCS(trunc)
AC_CHECK_FUNCS(nearbyint)
AC_CHECK_FUNCS(pwrite)
AC_FUNC_STRFTIME
AC_FUNC_GETTEXT
AC_STACKDIRECTION
AC_FUNC_ALLOCA

AC_SECOND_CONNECT_TO_REFUSED_PORT(BK_SECOND_REFUSED_CONNECT_ERRNO)

AC_DEV_FD
AC_PROCFS

# these are built-ins to most shells, but in case of stupidity check anyhow
AC_PATH_PROG(TRUE, true)
AC_PATH_PROG(FALSE, false)
AC_PATH_PROG(TEST, test)

AC_PATH_PROG(RM, rm)
AC_PATH_PROG(TOUCH, touch)
AC_PATH_PROG(CP, cp)
AC_PATH_PROGS(MV, mv cp, $FALSE)
AC_PATH_PROG(MKDIR, mkdir, $FALSE)
AC_PATH_PROG(LS, ls, $FALSE)
AC_PATH_PROG(GTAR, gtar, $FALSE)
AC_PATH_PROGS(TAR, gtar tar, $FALSE)
AC_PATH_PROG(GZIP, gzip, $FALSE)
AC_PATH_PROG(CVS, cvs, $FALSE)
AC_PATH_PROG(GIT, git, $FALSE)
AC_PATH_PROG(GITS, gits, $FALSE)
AC_PATH_PROG(UNAME, uname, $FALSE)
AC_PATH_PROG(CHMOD, chmod, $FALSE)
AC_PATH_PROG(JOIN, join, $FALSE)
AC_PATH_PROG(FMT, fmt, $FALSE)

AC_PATH_PROG(CAT, cat, $FALSE)
AC_PATH_PROG(BASENAME, basename, $FALSE)

AC_PATH_PROG(SED, sed, $FALSE)
# prefer perl in /usr/bin to /bin or other; export PERL=/bin/perl if you care
AC_PATH_PROG(PERL, perl, $FALSE, /usr/bin:$PATH)
AC_PATH_PROG(GREP, grep, $FALSE)
AC_PATH_PROG(FIND, find, $FALSE)
AC_PATH_PROG(SORT, sort, $FALSE)
AC_PATH_PROG(UNIQ, uniq, $FALSE)
AC_PATH_PROG(XARGS, xargs, $FALSE)
AC_PATH_PROG(EGREP, egrep, $FALSE)
AC_PATH_PROG(TEE, tee, $FALSE)
AC_PATH_PROG(EXPR, expr, $FALSE)

AC_PATH_PROG(AR, ar)

AC_PATH_PROG(XML2CONFIG, xml2-config, $FALSE)
if test "$ac_cv_path_XML2CONFIG" != "$FALSE"
then
	AC_SUBST(HAVE_LIBXML2,true)
fi

# not everybody puts /usr/local/gnu/bin in their PATH
AC_PATH_PROG(BGCC, bgcc, $FALSE, $PATH:/usr/local/gnu/bin)

# Look for PARASOFT directory if $PARASOFT is not found
if test "X$PARASOFT" '=' "X"
then
	if test -d "/usr/local/parasoft"
	then
		export PARASOFT="/usr/local/parasoft"
	fi
fi

if test \! -d $PARASOFT
then
	export PARASOFT=/path/to/nowhere
fi

AC_SUBST(PARASOFT)
AC_PATH_PROG(INSURE, insure, $FALSE, $PATH:$PARASOFT/bin:/usr/local/parasoft/bin)
AC_PATH_PROG(JTEST, jtest, $FALSE, $PATH:$PARASOFT/bin:/usr/local/parasoft/bin)

# note - don't use $PATH_SEPARATOR in this file; we always use Cygwin make

# Stock RedHat systems have Kaffe (not Sun Java) in /usr/bin, so prefix
# $JAVA_HOME/bin:/usr/java/jdk/bin (hope no java* in /bin if JAVA_HOME unset!)
AC_PATH_PROG(JAVAC, javac, $FALSE, $JAVA_HOME/bin:/usr/java/jdk/bin:$PATH)
AC_PATH_PROG(JAR, jar, $FALSE, $JAVA_HOME/bin:/usr/java/jdk/bin:$PATH)
AC_PATH_PROG(JAVADOC, javadoc, $FALSE, $JAVA_HOME/bin:/usr/java/jdk/bin:$PATH)

AC_PATH_PROG(JHINDEXER, jhindexer, $FALSE, $PATH:/usr/local/java/bin)

AC_PATH_PROG(DOXYGEN, doxygen, $FALSE)
AC_CHECK_PROG(HAVE_DOT, dot, YES, NO)
AC_PATH_PROGS(BROWSER, browser netscape konqueror mozilla iexplore.exe lynx,
	      $FALSE, $PATH:/cygdrive/c/progra~1/intern~1)

AC_PATH_PROG(HEAD, head, $FALSE)

AC_CHECK_FILES(/proc/net/route)

AC_PATH_PROGS(OINKMASTER, [oinkmaster.pl oinkmaster], $FALSE, [/usr/bin:/usr/sbin])
AC_PATH_PROG(ADDSID, addsid.pl, $FALSE, [/usr/bin:/usr/share/oinkmaster])
AC_PATH_PROG(CREATE_SIDMAP, create-sidmap.pl, $FALSE, [/usr/bin:/usr/share/oinkmaster])
AC_BARE_EMPTY_LOOPS_OK
if test "$HAVE_PTHREADS" = "true" && test "$ac_cv_bare_empty_loops_ok" = "no"; then
   AC_PTHREAD_WITH_BAD_EMPTY_LOOPS
fi

if test "$ac_cv_search_gethostbyname2" = "no"
then
  AC_MSG_WARN([you will not be able to resolve IPv6 names])
fi

AC_DEFINE_UNQUOTED(PATH_SEPARATOR, "$PATH_SEPARATOR")

# test for oracle <TODO>Should be a macro in aclocal.m4</TODO>
echo $ECHO_N "checking for Oracle... $ECHO_C" >&6
if test "" = "${ORACLE_HOME}"; then
  ORACLE_HOME=/proj/oracle; export ORACLE_HOME
fi
if test -x ${ORACLE_HOME}/bin/xml && ${ORACLE_HOME}/bin/xml -h>/dev/null 2>&1
then
  AC_DEFINE(HAVE_ORACLE)
  HAVE_ORACLE=true; export HAVE_ORACLE
  AC_SUBST(ORACLE_HOME)
  echo "${ECHO_T}$ORACLE_HOME" >&6
else
  HAVE_ORACLE=false; export HAVE_ORACLE
  echo "${ECHO_T}no" >&6
fi
AC_SUBST(HAVE_ORACLE)

# Ruby-specific shared lib extension check
AC_PATH_PROG(RUBY, ruby, $FALSE)
if test "$RUBY" != "$FALSE"; then
  eval ruby_shlibext="."`$RUBY -r rbconfig -e "print RbConfig::MAKEFILE_CONFIG@<:@'DLEXT'@:>@"`
else
  eval ruby_shlibext=$shlibext
fi
AC_DEFINE_UNQUOTED(RUBY_SHLIBEXT, "$ruby_shlibext")
AC_SUBST(ruby_shlibext)


# Check if install supports -p
INSTALL_FILE=/tmp/bkinstall.$$

{
	if : > $INSTALL_FILE ; then
	   echo $ECHO_N "checking if install supports -p... $ECHO_C" >&6

	   if $INSTALL -p $INSTALL_FILE ${INSTALL_FILE}-; then
		 INSTALL_PRESERVE="-p"
		 echo "${ECHO_T}yes"
	   else
		 INSTALL_PRESERVE=""
		 echo "${ECHO_T}no"
	   fi
	   AC_SUBST(INSTALL_PRESERVE)
	   $RM -f $INSTALL_FILE ${INSTALL_FILE}-
	fi
} 2>/dev/null


# <TRICKY>In order for the pattern rule in bkmk/Makefile to work right, all
# files generated by configure from .in files must have exactly *one* . in
# the name (not counting the .in).</TRICKY>

AC_CONFIG_HEADER(libbk_autoconf.h)
AC_CONFIG_FILES(Make.config getcwd.sh bk.conf)
AC_OUTPUT
